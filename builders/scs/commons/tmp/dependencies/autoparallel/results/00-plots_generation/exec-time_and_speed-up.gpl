#!/bin/gnuplot

# This GNU Plot script requires the following variables:
#   - application : Name of the application
#   - ysize : Distance between y entries on the Execution Time plot
# 
# Call example:
#   gnuplot -e "application='cholesky'" -e "ysize=100" exec-time_and_speed-up.gpl
#

reset

# Output format
set terminal pdf enhanced font "sans,16"

# Set output file
set output "output/" . application . ".pdf"

# Set multi plot
set multiplot layout 2, 1 margins 0.15, 0.98, 0.1, 0.99 spacing 0, 0.05
set tmargin 2
#set bmargin 0
#set lmargin 1
#set rmargin 1


#
# PLOT 1: EXECUTION TIME
#

#set title "Execution Time"

# Caption position
set key outside top center horizontal noreverse noenhanced autotitle nobox

# Set labels
#set xlabel "#Workers (#cores)"
unset xtics
set ylabel "Execution Time (s)"

# Style
set style histogram errorbars linewidth 1 gap 1
set style data histograms

set boxwidth 0.9 absolute
set style fill solid 0.5 
set bars front

set yrange [0:*]
set ytics ysize
set grid ytics

# Plot
plot "data/" . application . ".data" using 5:3:4:xticlabels(1) title "UserParallel" linecolor rgb "#004586", \
        '' using 9:7:8 title "AutoParallel" lt 1 lc rgb "#3cb371", \
        '' using 5:xticlabels(1) with lines linecolor rgb "#2b5272" title "", \
        '' using 9:xticlabels(1) with lines linecolor rgb "#29aa63" title ""
 

#
# PLOT 2: SPEED-UP
#

#set title "Speed-Up"

# Caption position
unset key
#set key inside right top vertical noreverse noenhanced autotitle nobox

# Set labels
set xlabel "#Workers (#cores)"
set xtics
set ylabel "Speed-up (u)"

# Style
set style histogram cluster gap 1
set style data histograms

set boxwidth 0.9 absolute
set style fill solid 0.5
set bars front

set yrange [0.5:4]
set logscale y 2
set ytics (0, 1, 2, 4)
set grid ytics

# Plot
plot "data/" . application . ".data" using 2:xticlabels(1) title "UserParallel" linecolor rgb "#004586", \
        '' using 6 title "AutoParallel" lt 1 lc rgb "#3cb371", \
        '' using 2:xticlabels(1) with lines linecolor rgb "#2b5272" title "", \
        '' using 6:xticlabels(1) with lines linecolor rgb "#29aa63" title ""

unset multiplot
